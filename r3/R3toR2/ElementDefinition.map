map "http://hl7.org/fhir/StructureMap/ElementDefinition3to2" = "R3 to R2 Conversion for ElementDefinition"

uses "http://hl7.org/fhir/StructureDefinition/ElementDefinition" alias ElementDefinition as source
uses "http://hl7.org/fhir/DSTU2/StructureDefinition/ElementDefinition" alias ElementDefinitionR2 as target

imports "http://hl7.org/fhir/StructureMap/*3to2"

group for type+types  ElementDefinition
  input srcR : Resource as source
  input src : ElementDefinition as source
  input tgt : ElementDefinitionR2 as target

  "ElementDefinition-extensions" : for src.extension make tgt.extension
  "ElementDefinition-path" : for src.path make tgt.path
  "ElementDefinition-name" : for src.sliceName make tgt.name
  "ElementDefinition-representation" : for src.representation where $this = 'xmlAttr' make tgt.representation
  "ElementDefinition-label" : for src.label make tgt.label
  "ElementDefinition-code" : for src.code make tgt.code
  "ElementDefinition-slicing" : for src.slicing as vs0 make tgt.slicing as vt0 then {
    "ElementDefinition.slicing-discriminator" : for vs0.discriminator as vd1 then edDiscriminator(vd1, vt0)
    "ElementDefinition.slicing-description" : for vs0.description make vt0.description
    "ElementDefinition.slicing-ordered" : for vs0.ordered make vt0.ordered
    "ElementDefinition.slicing-rules" : for vs0.rules make vt0.rules
  }

  "ElementDefinition-short" : for src.short make tgt.short
  "ElementDefinition-definition" : for src.definition make tgt.definition
  "ElementDefinition-comments" : for src.comment make tgt.comments
  "ElementDefinition-requirements" : for src.requirements make tgt.requirements
  "ElementDefinition-alias" : for src.alias make tgt.alias
  "ElementDefinition-min" : for src.min make tgt.min
  "ElementDefinition-max" : for src.max make tgt.max
  "ElementDefinition-base" : for src.base as vs0 make tgt.base as vt0 then {
    "ElementDefinition.base-path" : for vs0.path make vt0.path
    "ElementDefinition.base-min" : for vs0.min make vt0.min
    "ElementDefinition.base-max" : for vs0.max make vt0.max
  }

  "ElementDefinition-type" : for src.type as vs0 make tgt.type as vt0 then type(vs0, vt0)

  "ElementDefinition-type-1st" : for src where path.contains('.').not() and src.type.empty() and src.contentReference.empty() and srcR.is(StructureDefinition) make tgt.type as vt0 then {
    "ElementDefinition.type-code" : for srcR.baseDefinition as base make vt0.code = (base.substring(40))
  }

  "ElementDefinition-contentReference" : for src.contentReference make tgt.nameReference
  "ElementDefinition-defaultValue" : for src.defaultValue make tgt.defaultValue
  "ElementDefinition-meaningWhenMissing" : for src.meaningWhenMissing make tgt.meaningWhenMissing
  "ElementDefinition-example" : for src.example first as exv then {
    "ElementDefinition-example-v" : for exv.value make tgt.example
  }
  "ElementDefinition-fixed" : for src.fixed make tgt.fixed
  "ElementDefinition-pattern" : for src.pattern make tgt.pattern
  "ElementDefinition-minValue" : for src.minValue make tgt.minValue
  "ElementDefinition-maxValue" : for src.maxValue make tgt.maxValue
  "ElementDefinition-maxLength" : for src.maxLength make tgt.maxLength
  "ElementDefinition-condition" : for src.condition make tgt.condition
  "ElementDefinition-constraint" : for src.constraint as vs0 make tgt.constraint as vt0 then constraint(vs0, vt0)
  "ElementDefinition-mustSupport" : for src.mustSupport make tgt.mustSupport
  "ElementDefinition-isModifier" : for src.isModifier make tgt.isModifier
  "ElementDefinition-isSummary" : for src.isSummary make tgt.isSummary
  "ElementDefinition-binding" : for src.binding as vs0 make tgt.binding as vt0 then binding(vs0, vt0)
  "ElementDefinition-mapping" : for src.mapping as vs0 make tgt.mapping as vt0 then mapping(vs0, vt0)
endgroup

group edDiscriminator extends Element
  input src as source
  input tgt as target
  
  "ElementDefinition.binding-strength" : for src.path where type = 'value' make tgt.discriminator
endgroup

group binding extends Element
  input src as source
  input tgt as target
  
  "ElementDefinition.binding-strength" : for src.strength make tgt.strength
  "ElementDefinition.binding-description" : for src.description make tgt.description
  "ElementDefinition.binding-valueSet" : for src.valueSet make tgt.valueSet
endgroup

group type extends Element
  input src as source
  input tgt as target

  "ElementDefinition.type-code" : for src.code make tgt.code
  "ElementDefinition.type-profile1" : for src.profile where src.targetProfile.empty() make tgt.profile
  "ElementDefinition.type-profile2" : for src.targetProfile make tgt.profile
  "ElementDefinition.type-aggregation" : for src.aggregation make tgt.aggregation
endgroup

group constraint extends Element
  input src as source
  input tgt as target
  
  "ElementDefinition.constraint-key" : for src.key make tgt.key
  "ElementDefinition.constraint-requirements" : for src.requirements make tgt.requirements
  "ElementDefinition.constraint-severity" : for src.severity make tgt.severity
  "ElementDefinition.constraint-human" : for src.human make tgt.human
  "ElementDefinition.constraint-xpath" : for src.xpath make tgt.xpath
endgroup

group mapping extends Element
  input src as source
  input tgt as target
  
  "ElementDefinition.mapping-identity" : for src.identity make tgt.identity
  "ElementDefinition.mapping-language" : for src.language make tgt.language
  "ElementDefinition.mapping-map" : for src.map make tgt.map
endgroup
