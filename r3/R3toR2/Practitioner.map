map "http://hl7.org/fhir/StructureMap/Practitioner3to2" = "R3 to R2 Conversion for Practitioner"

uses "http://hl7.org/fhir/StructureDefinition/Practitioner" alias Practitioner as source
uses "http://hl7.org/fhir/DSTU2/StructureDefinition/Practitioner" alias PractitionerR2 as target

imports "http://hl7.org/fhir/StructureMap/*3to2"

group for type+types  Practitioner extends DomainResource
  input src : Practitioner as source
  input tgt : PractitionerR2 as target

  "Practitioner-identifier" : for src.identifier make tgt.identifier
  "Practitioner-active" : for src.active make tgt.active
  "Practitioner-name" : for src.name make tgt.name
  "Practitioner-telecom" : for src.telecom make tgt.telecom
  "Practitioner-address" : for src.address make tgt.address
  "Practitioner-gender" : for src.gender make tgt.gender
  "Practitioner-birthDate" : for src.birthDate make tgt.birthDate
  "Practitioner-photo" : for src.photo make tgt.photo
  "Practitioner-role" : for search('/PractitionerRole?practitioner='+src.id) : PractitionerRole as vs0 make tgt.practitionerRole as vt0 then prcRole(vs0, vt0)
  "Practitioner-qualification" : for src.qualification as vs0 make tgt.qualification as vt0 then prcQualification(vs0, vt0) 
  "Practitioner-communication" : for src.communication make tgt.communication
endgroup

group prcRole extends BackboneElement
  input src as source
  input tgt as target

  "Practitioner.role-organization" : for src.organization make tgt.managingOrganization
  "Practitioner.role-code" : for src.code make tgt.role
  "Practitioner.role-specialty" : for src.specialty make tgt.specialty
  "Practitioner.role-period" : for src.period make tgt.period
  "Practitioner.role-location" : for src.location make tgt.location
  "Practitioner.role-healthcareService" : for src.healthcareService make tgt.healthcareService
endgroup
  
group prcQualification extends BackboneElement
  input src as source
  input tgt as target

  "Practitioner.qualification-identifier" : for src.identifier make tgt.identifier
  "Practitioner.qualification-code" : for src.code make tgt.code
  "Practitioner.qualification-period" : for src.period make tgt.period
  "Practitioner.qualification-issuer" : for src.issuer make tgt.issuer
endgroup
