map "http://hl7.org/fhir/StructureMap/Medication2to3" = "R2 to R3 Conversions for Medication"

uses "http://hl7.org/fhir/DSTU2/StructureDefinition/Medication" alias MedicationR2 as source
uses "http://hl7.org/fhir/StructureDefinition/Medication" alias Medication as target

imports "http://hl7.org/fhir/StructureMap/*2to3"

group for type+types  Medication extends DomainResource
  input src : MedicationR2 as source
  input tgt : Medication as target

  "Medication-code" : for src.code make tgt.code
  "Medication-isBrand" : for src.isBrand make tgt.isBrand
  "Medication-manufacturer" : for src.manufacturer make tgt.manufacturer
  
  "Medication-product" : for src.product as vs0 then medProduct(vs0, tgt)
  "Medication-package" : for src.package as vs0 make tgt.package as vt0 then medPackage(vs0, vt0)
endgroup

group medProduct extends BackboneElement
  input src as source
  input tgt as target

  "Medication.product-form" : for src.form make tgt.form
  "Medication.product-ingredient" : for src.ingredient as vs0 make tgt.ingredient as vt0 then medIngredient(vs0, vt0)
  "Medication.product-batch" : for src.batch as vs0 make tgt.package as pkg, pkg.batch as vt0 then medBatch(vs0, vt0)
endgroup

group medIngredient extends BackboneElement
  input src as source
  input tgt as target

  "Medication.product.batch-item" : for src.item make tgt.item
  "Medication.product.batch-amount" : for src.amount make tgt.amount
endgroup

group medBatch extends BackboneElement
  input src as source
  input tgt as target

  "Medication.product.batch-lotNumber" : for src.lotNumber make tgt.lotNumber
  "Medication.product.batch-expirationDate" : for src.expirationDate make tgt.expirationDate
endgroup

group medPackage extends BackboneElement
  input src as source
  input tgt as target

  "Medication.package-container" : for src.container make tgt.container
  "Medication.package-content" : for src.content as vs0 make tgt.content as vt0 then medContent(vs0, vt0)
endgroup

group medContent extends BackboneElement
  input src as source
  input tgt as target

  "Medication.package.content-item" : for src.item make tgt.item
  "Medication.package.content-amount" : for src.amount make tgt.amount
endgroup

