map "http://hl7.org/fhir/StructureMap/QuestionnaireResponse2to3" = "R2 to R3 Conversions for QuestionnaireResponse"

uses "http://hl7.org/fhir/DSTU2/StructureDefinition/QuestionnaireResponse" alias QuestionnaireResponseR2 as source
uses "http://hl7.org/fhir/StructureDefinition/QuestionnaireResponse" alias QuestionnaireResponse as target

imports "http://hl7.org/fhir/StructureMap/*2to3"

group for type+types  QuestionnaireResponse extends DomainResource
  input src : QuestionnaireResponseR2 as source
  input tgt : QuestionnaireResponse as target

  "QuestionnaireResponse-identifier" : for src.identifier make tgt.identifier
  "QuestionnaireResponse-questionnaire" : for src.questionnaire make tgt.questionnaire
  "QuestionnaireResponse-status" : for src.status make tgt.status
  "QuestionnaireResponse-subject" : for src.subject make tgt.subject
  "QuestionnaireResponse-author" : for src.author make tgt.author
  "QuestionnaireResponse-authored" : for src.authored make tgt.authored
  "QuestionnaireResponse-source" : for src.source make tgt.source
  "QuestionnaireResponse-encounter" : for src.encounter make tgt.context
  
  "QuestionnaireResponse-item" : for src.group as vs0 make tgt.item as vt0 then qrGroup(vs0, vt0)
endgroup

group qrGroup extends BackboneElement
  input src as source
  input tgt as target

  "QuestionnaireResponse.group-linkId" : for src.linkId make tgt.linkId
  "QuestionnaireResponse.group-title" : for src.title where src.text.empty() make tgt.text
  "QuestionnaireResponse.group-text" : for src.text make tgt.text
  "QuestionnaireResponse.group-subject" : for src.subject make tgt.subject
  "QuestionnaireResponse.group-group" : for src.group as vs0 make tgt.item as vt0 then qrGroup(vs0, vt0)
  "QuestionnaireResponse.group-question" : for src.question as vs0 make tgt.item as vt0 then qrQuestion(vs0, vt0)
endgroup

group qrQuestion extends BackboneElement
  input src as source
  input tgt as target

  "QuestionnaireResponse.group.question-linkId" : for src.linkId make tgt.linkId
  // drop title
  "QuestionnaireResponse.group.question-text" : for src.text make tgt.text
  "QuestionnaireResponse.group.question-answer" : for src.answer as vs0 make tgt.answer as vt0 then qrAnswer(vs0, vt0)
endgroup

group qrAnswer extends BackboneElement
  input src as source
  input tgt as target

  "QuestionnaireResponse.group.question.answer-value" : for src.value make tgt.value 
  "QuestionnaireResponse.group.question.answer-group" : for src.group as vs0 make tgt.item as vt0 then qrGroup(vs0, vt0)
endgroup
